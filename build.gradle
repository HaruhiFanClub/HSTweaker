buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

group = 'com.haruhifanclub'
archivesBaseName = 'hstweaker'
version = "${minecraft_version}-${hstweaker_version}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: "${mappings_channel}", version: "${mappings_version}"
    runs {
        client {
            workingDirectory project.file('run').canonicalPath
            property 'file.encoding', 'UTF-8'
            property 'user.language', 'en'
            property 'log4j.configurationFile', 'log4j2.xml'
            property 'mixin.debug', 'true'
            arg '-mixin.config=hstweaker.mixin.json'
            mods {
                hstweaker {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run').canonicalPath
            property 'file.encoding', 'UTF-8'
            property 'user.language', 'en'
            property 'log4j.configurationFile', 'log4j2.xml'
            property 'mixin.debug', 'true'
            arg '-mixin.config=hstweaker.mixin.json'
            mods {
                hstweaker {
                    source sourceSets.main
                }
            }
        }
    }
}


repositories {
    maven {
        url "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url "https://maven.pkg.github.com/auioc/arnicalib-mcmod"
        credentials {
            username = System.getenv("GITHUB_ACTOR") ? System.getenv("GITHUB_ACTOR") : System.getenv("GITHUB_USERNAME")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"
    testAnnotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"

    implementation "org.auioc.mcmod:arnicalib:${minecraft_version}-${arnicalib_version}:forgelib"

    if (getVar('GITHUB_ACTOR') == null) {
        runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}")
        runtimeOnly fg.deobf("curse.maven:jade-324717:${jade_version}")
    }
}

mixin {
    add sourceSets.main, "hstweaker.mixin-refmap.json"
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : 'hstweaker',
                "Specification-Vendor"    : 'HaruhiFanClub',
                "Specification-Version"   : '1',
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${hstweaker_version}",
                "Implementation-Vendor"   : "HaruhiFanClub",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder": 0,
                "MixinConfigs": "hstweaker.mixin.json",
                "HSTweaker-Version": "hstweaker-${minecraft_version}-${hstweaker_version}-${getVar('CI_VERSION')}"
        ])
    }
}

jar.finalizedBy('reobfJar')


def getVar(String var_name) {
    return System.getenv(var_name)
}
